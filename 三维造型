import peasy.*;
import controlP5.*;

float boxSize1 = 50.0; 
float boxSize2 = 70.0; 
float distance = 150.0; 
float sw = 1; 
float rotation1 = 0.0; 
float rotation2 = 0.0; 

PeasyCam cam;
ControlP5 cp;

void setup() {
  size(800, 800, P3D);
  background(0);
  cam = new PeasyCam(this, 200);
  UI();
}

void draw() {
  background(0);
  strokeWeight(sw);
  stroke(200, 200, 0);

  pushMatrix();
  translate(-distance / 2, 0, 0);
  rotateY(rotation1);
  fill(255);
  box(boxSize1);
  popMatrix();

  pushMatrix();
  translate(distance / 2, 0, 0);
  rotateY(rotation2);
  fill(255, 0, 0);
  box(boxSize2);
  popMatrix();

  cam.beginHUD();
  cp.draw();
  cam.endHUD();

  if (mouseX < 300 && mouseY < 300) {
    cam.setActive(false);
  } else {
    cam.setActive(true);
  }
}

void UI() {
  cp = new ControlP5(this, createFont("微软雅黑", 16));
  cp.addSlider("boxSize1").setRange(0, 200).setValue(50).setPosition(50, 50).setSize(200, 30).setLabel("正方体1大小");
  cp.addSlider("boxSize2").setRange(0, 200).setValue(70).setPosition(50, 50 + 30 + 10).setSize(200, 30).setLabel("正方体2大小");
  cp.addSlider("distance").setRange(0, 300).setValue(150).setPosition(50, 50 + 30 + 10 + 30 + 10).setSize(200, 30).setLabel("两个正方体的距离");
  cp.addSlider("sw").setRange(0, 20).setValue(1).setPosition(50, 50 + 30 + 10 + 30 + 10 + 30 + 10).setSize(200, 30).setLabel("笔触宽度");
  cp.addSlider("rotation1").setRange(0, TWO_PI).setValue(0).setPosition(50, 50 + 30 + 10 + 30 + 10 + 30 + 10 + 30 + 10).setSize(200, 30).setLabel("正方体1旋转角度");
  cp.addSlider("rotation2").setRange(0, TWO_PI).setValue(0).setPosition(50, 50 + 30 + 10 + 30 + 10 + 30 + 10 + 30 + 10 + 30 + 10).setSize(200, 30).setLabel("正方体2旋转角度");

  cp.setAutoDraw(false);
}
